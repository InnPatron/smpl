// program -> decl_stmt
// decl_stmt -> fn_decl | struct_decl
// fn_decl -> "fn" ident (arg_list) { stmt_seq }
// struct_decl -> "struct" ident { field_list }
// stmt_seq -> stmt+
// stmt -> expr ";" | expr_stmt
// expr -> bin_expr | uni_expr | return | break | continue | literal | ident
// | "(" expr ")"
// expr_stmt -> if_expr | while_expr | local_var_decl | assignment
//
// local_var_decl -> path assignment
//
// assignment -> ident "=" expr ";"
//
// bin_expr -> expr bin_op expr
// bin_op -> "+" | "-" | "*" | "/" | "%"
//
// uni_expr -> uni_op expr
// uni_op -> "-" | "*" | "&" | "!"
//
// if_expr -> if expr { stmt_seq }
// while_expr while expr { stmt_seq }
//
// literal -> bool | number | string
// bool -> "true | "false"
//
// return -> "return" expr
// break -> "break" expr
// continue -> "continue"
// 
// path -> ident path_tail
// path_tail -> "." ident | path_tail | E

use std::str::FromStr;
use smpl_type::SmplType;
use ast::*;
use ascii::*;

grammar;

pub Program: Program = {
	<decl: DeclStmt+> => {
		Program(decl)
	}
};

DeclStmt: DeclStmt = {
	<f: FnDecl> => f.into(),
	<s: StructDecl> => s.into(),
};

pub FnDecl: Function = {
	"fn" <name: Ident> "(" <args: FnArgList?> ")" <return_type: ("->" Path)?> <b: Block> => {
		Function {
			name: name,
			args: args,
			return_type: return_type.map(|r| r.1),
			body: AstNode::untyped(b),
		}
	}
};

FnArgList: Vec<FnArg> = {
	<first: FnArg> <tail: FnArgTail?> => {
		let mut r = Vec::new();
		r.push(first);
		if let Some(v) = tail {
			r.extend(v);
		}
		r
	},
};

FnArgTail: Vec<FnArg> = {
	<args: ("," <FnArg>)+> => args,
};

FnArg: FnArg = {
	<arg_type: Path> <name: Ident> => {
		FnArg {
			name: name,
			arg_type: arg_type,
		}
	}
};

pub StructDecl: Struct = {
	"struct" <name: Ident> "{" <fields: (<StructField> ",")*> "}" => {
		Struct {
			name: name,
			body: StructBody(fields)
		}
	}
};

StructField: StructField = {
	<name: Ident> ":" <field_type: Path> => {
		StructField {
			name: name,
			field_type: field_type,
		}
	}
};

pub Stmt: Stmt = {
	<e: Expr> ";" => Stmt::Expr(e),
	<e: ExprStmt> => Stmt::ExprStmt(e),
};

Block: Block = {
	"{" <s: Stmt*> "}" => Block(s)
};

pub ExprStmt: ExprStmt = {
	<if_stmt: IfStmt> => ExprStmt::If(if_stmt),
	<while_stmt: WhileStmt> => ExprStmt::While(while_stmt),
	<local_var_decl: LocalVarDecl> => ExprStmt::LocalVarDecl(local_var_decl),
	<assignment: Assignment> => ExprStmt::Assignment(assignment),
};

LocalVarDecl: LocalVarDecl = {
	<var_type: Path> <name: Ident> "=" <e: Expr> ";" => {
		LocalVarDecl {
			var_type: var_type,
			var_name: name,
			var_init: AstNode::untyped(e)
		}
	},
};

Assignment: Assignment = {
	<name: Path> "=" <e: Expr> ";" => {
		Assignment {
			name: AstNode::untyped(name),
			value: AstNode::untyped(e),
		}
	}
};

IfStmt: If = {
	"if" <conditional: Expr> <block: Block> => {
		If {
			conditional: AstNode::untyped(conditional),
			block: block
		}
	},
};

WhileStmt: While = {
	"while" <conditional: Expr> <block: Block> => {
		While {
			conditional: AstNode::untyped(conditional),
			block: block
		}
	},
};

pub Expr: Expr = {	
	<c: CmpExpr> => c,
};

pub CmpExpr: Expr = {
	<lhs: CmpExpr> <op: CmpOpLowestPrecedence> <rhs: RelativeCmpBranch> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: op,
			lhs: AstNode::untyped(Box::new(lhs)),
			rhs: AstNode::untyped(Box::new(rhs)),
		}))
	},

	RelativeCmpBranch,
};

RelativeCmpBranch: Expr = {
	<lhs: RelativeCmpBranch> <op: CmpOpLowPrecedence> <rhs: EqualityCmpBranch> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: op,
			lhs: AstNode::untyped(Box::new(lhs)),
			rhs: AstNode::untyped(Box::new(rhs)),
		}))
	},

	EqualityCmpBranch,
};

EqualityCmpBranch: Expr = {
	<lhs: EqualityCmpBranch> <op: CmpOpGreaterPrecedence> <rhs: MathExpr> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: op,
			lhs: AstNode::untyped(Box::new(lhs)),
			rhs: AstNode::untyped(Box::new(rhs)),
		}))
	},

	<MathExpr>,
};

CmpOpLowestPrecedence: BinOp = {
	"&&" => BinOp::LogicalAnd,
	"||" => BinOp::LogicalOr,
};

CmpOpGreaterPrecedence: BinOp = {
	">" => BinOp::Greater,
	"<" => BinOp::Lesser,
	">=" => BinOp::GreaterEq,
	"<=" => BinOp::LesserEq,
};

CmpOpLowPrecedence: BinOp = {
	"==" => BinOp::Eq,
	"!=" => BinOp::InEq,
};

pub MathExpr: Expr = {
	<lhs: MathExpr> "+" <rhs: Factor> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: BinOp::Add,
			lhs: AstNode::untyped(Box::new(lhs.into())),
			rhs: AstNode::untyped(Box::new(rhs.into())),
		}))
	},

	<lhs: MathExpr> "-" <rhs: Factor> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: BinOp::Sub,
			lhs: AstNode::untyped(Box::new(lhs.into())),
			rhs: AstNode::untyped(Box::new(rhs.into())),
		}))
	},

	Factor,
};

Factor: Expr = {
	<lhs: Factor> "*" <rhs: Term> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: BinOp::Mul,
			lhs: AstNode::untyped(Box::new(lhs.into())),
			rhs: AstNode::untyped(Box::new(rhs)),
		}))
	},

	<lhs: Factor> "/" <rhs: Term> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: BinOp::Div,
			lhs: AstNode::untyped(Box::new(lhs.into())),
			rhs: AstNode::untyped(Box::new(rhs)),
		}))
	},

	<lhs: Factor> "%" <rhs: Term> => {
		Expr::Bin(AstNode::untyped(BinExpr {
			op: BinOp::Mod,
			lhs: AstNode::untyped(Box::new(lhs.into())),
			rhs: AstNode::untyped(Box::new(rhs)),
		}))
	},
	<Term>
};

Term: Expr = {
	"(" <e: Expr> ")" => e.into(),
	<e: ExprLeaf> => e,
};

// end node rule which all other Expr related rules end in 
ExprLeaf: Expr = {
	"(" <u: UniExpr> ")" => Expr::Uni(AstNode::untyped(u)),
	<i: Ident> => Expr::Ident(AstNode::untyped(i)),
	<l: Literal> => Expr::Literal(l),
	<f: FnCall> => Expr::FnCall(AstNode::untyped(f)),
	<b: Bool> => b,
};

Bool: Expr = {
	"true" => Expr::Literal(AstNode::typed(Literal::Bool(true), SmplType::Bool)),
	"false" => Expr::Literal(AstNode::typed(Literal::Bool(false), SmplType::Bool)),
};

FnCall: FnCall = {
	<name: Ident> "(" <args: FnCallArgs?> ")" => {
		FnCall {
			name: name,
			args: args,
		}
	}
};

FnCallArgs: Vec<Expr> = {
	<first: Expr> <tail: FnCallArgsTail?> => {
		let mut a = Vec::new();
		a.push(first);
		match tail {
			None => a,
			Some(t) => {
				a.extend(t);
				a
			}
		}
	}
};

FnCallArgsTail: Vec<Expr> = {
	<args: ("," <Expr>)+> => args
};

BinOp: BinOp = {
	"+" => BinOp::Add,
	"-" => BinOp::Sub,
	"*" => BinOp::Mul,
	"/" => BinOp::Div,
	"%" => BinOp::Mod,
};

UniExpr: UniExpr = {
	<op: UniOp> <e: Expr> => UniExpr { op: op, expr: AstNode::untyped(Box::new(e)) },
};

UniOp: UniOp = {
	"-" => UniOp::Negate,
	"!" => UniOp::LogicalInvert,
	"&" => UniOp::Ref,
	"*" => UniOp::Deref,
};

Literal: AstNode<Literal> = {
	<number: r"[0-9]+[.[0-9]+]?"> => AstNode::untyped(Literal::Number(number.to_string())),
	"\"" <string: "r[.]*"> "\"" => AstNode::typed(Literal::String(string.to_string()), 
					SmplType::String),
};

Ident: Ident = {
	<ident: r"[A-Za-z_][_A-Za-z0-9]*"> => { Ident(AsciiString::from_str(ident).unwrap()) }
};

Path: Path = {
	<first: Ident> <v:("." <Ident>)*> => {
		let mut p = Vec::new();
		p.push(first);
		p.extend_from_slice(&v);
		Path(p)
	}
};

pub Keyword: Keyword = {
	"return" => Keyword::Return,
	"if" => Keyword::If,
	"while" => Keyword::While,
	"break" => Keyword::Break,
	"struct" => Keyword::Struct,
	"fn" => Keyword::Function,
};
