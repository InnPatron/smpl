// program -> decl_stmt
// decl_stmt -> fn_decl | struct_decl
// fn_decl -> "fn" ident (arg_list) { stmt_seq }
// struct_decl -> "struct" ident { field_list }
// stmt_seq -> stmt+
// stmt -> expr ";" | expr_stmt
// expr -> bin_expr | uni_expr | return | break | continue | literal | ident
// | "(" expr ")"
// expr_stmt -> if_expr | while_expr | local_var_decl | assignment
//
// local_var_decl -> path assignment
//
// assignment -> ident "=" expr ";"
//
// bin_expr -> expr bin_op expr
// bin_op -> "+" | "-" | "*" | "/" | "%"
//
// uni_expr -> uni_op expr
// uni_op -> "-" | "*" | "&" | "!"
//
// if_expr -> if expr { stmt_seq }
// while_expr while expr { stmt_seq }
//
// literal -> bool | number | string
// bool -> "true | "false"
//
// return -> "return" expr
// break -> "break" expr
// continue -> "continue"
// 
// path -> ident path_tail
// path_tail -> "." ident | path_tail | E

use std::str::FromStr;
use smpl_type::SmplType;
use ast::*;
use ascii::*;

grammar;

pub DeclStmt: DeclStmt = {
	<f: FnDecl> => f.into(),
	<s: StructDecl> => s.into(),
};

pub FnDecl: Function = {
	"fn" <name: Ident> "(" <args: (<FnArg> ",")*> ")" => {
		Function {
			name: name,
			args: args,
			body: unimplemented!(),
		}
	}
};

pub FnArg: FnArg = {
	<arg_type: Ident> <name: Ident> => {
		FnArg {
			name: name,
			arg_type: arg_type,
		}
	}
};

pub StructDecl: Struct = {
	"struct" <name: Ident> "{" <fields: (<StructField> ",")*> "}" => {
		Struct {
			name: name,
			body: StructBody(fields)
		}
	}
};

StructField: StructField = {
	<field_type: Ident> ":" <name: Ident> => {
		StructField {
			name: name,
			field_type: field_type,
		}
	}
};

pub Stmt: Stmt = {
	<e: Expr> ";" => Stmt::Expr(e),
	<e: ExprStmt> => Stmt::ExprStmt(e),
};

pub Block: Block = {
	"{" <s: Stmt*> "}" => Block(s)
};

pub ExprStmt: ExprStmt = {
	<if_stmt: IfStmt> => ExprStmt::If(if_stmt),
	<while_stmt: WhileStmt> => ExprStmt::While(while_stmt),
};

pub IfStmt: If = {
	"if" <conditional: Expr> <block: Block> => {
		If {
			conditional: conditional,
			block: block
		}
	},
};

pub WhileStmt: While = {
	"while" <conditional: Expr> <block: Block> => {
		While {
			conditional: conditional,
			block: block
		}
	},
};

pub Expr: Expr = {	
	<b: BinExpr> => b.into(),
	<c: CmpExpr> => c,
};

pub CmpExpr: Expr = {
	<lhs: CmpExpr> <op: CmpOpGreaterPrecedence> <rhs: BinExpr> => {
		BinExpr {
			op: op,
			lhs: Box::new(lhs.into()),
			rhs: Box::new(rhs.into()),
		}.into()
	},

	<lhs: CmpExpr> <op: CmpOpLowerPrecedence> <rhs: BinExpr> => {
		BinExpr {
			op: op,
			lhs: Box::new(lhs.into()),
			rhs: Box::new(rhs.into()),
		}.into()
	},
};

pub CmpOpGreaterPrecedence: BinOp = {
	">" => BinOp::Greater,
	"<" => BinOp::Lesser,
	">=" => BinOp::GreaterEq,
	"<=" => BinOp::LesserEq,
};

pub CmpOpLowerPrecedence: BinOp = {
	"==" => BinOp::Eq,
	"!=" => BinOp::InEq,
};

pub BinExpr: Expr = {
	<lhs: BinExpr> "+" <rhs: Factor> => {
		BinExpr {
			op: BinOp::Add,
			lhs: Box::new(lhs.into()),
			rhs: Box::new(rhs.into()),
		}.into()
	},

	<lhs: BinExpr> "-" <rhs: Factor> => {
		BinExpr {
			op: BinOp::Sub,
			lhs: Box::new(lhs.into()),
			rhs: Box::new(rhs.into()),
		}.into()
	},

	Factor,
};

pub Factor: Expr = {
	<lhs: Factor> "*" <rhs: Term> => {
		BinExpr {
			op: BinOp::Mul,
			lhs: Box::new(lhs.into()),
			rhs: Box::new(rhs),
		}.into()
	},

	<lhs: Factor> "/" <rhs: Term> => {
		BinExpr {
			op: BinOp::Div,
			lhs: Box::new(lhs.into()),
			rhs: Box::new(rhs),
		}.into()
	},

	<lhs: Factor> "%" <rhs: Term> => {
		BinExpr {
			op: BinOp::Mod,
			lhs: Box::new(lhs.into()),
			rhs: Box::new(rhs),
		}.into()
	},
	<Term>
};

pub Term: Expr = {
	"(" <e: Expr> ")" => e.into(),
	<e: ExprLeaf> => e,
};

// end node rule which all other Expr related rules end in 
pub ExprLeaf: Expr = {
	"(" <u: UniExpr> ")" => u.into(),
	<i: Ident> => i.into(),
	<l: Literal> => Expr::Literal(l),
	<f: FnCall> => f.into(),
};

pub FnCall: FnCall = {
	<name: Ident> "(" <args: FnCallArgs?> ")" => {
		FnCall {
			name: name,
			args: args,
		}
	}
};

FnCallArgs: Vec<Expr> = {
	<first: Expr> <tail: FnCallArgsTail?> => {
		let mut a = Vec::new();
		a.push(first);
		match tail {
			None => a,
			Some(t) => {
				a.extend(t);
				a
			}
		}
	}
};

FnCallArgsTail: Vec<Expr> = {
	<args: ("," <Expr>)+> => args
};

BinOp: BinOp = {
	"+" => BinOp::Add,
	"-" => BinOp::Sub,
	"*" => BinOp::Mul,
	"/" => BinOp::Div,
	"%" => BinOp::Mod,
};

pub UniExpr: UniExpr = {
	<op: UniOp> <e: Expr> => UniExpr { op: op, expr: AstNode::untyped(Box::new(e)) },
};

pub UniOp: UniOp = {
	"-" => UniOp::Negate,
	"!" => UniOp::LogicalInvert,
	"&" => UniOp::Ref,
	"*" => UniOp::Deref,
};

pub Literal: AstNode<Literal> = {
	<number: r"[0-9]+[.[0-9]+]?"> => AstNode::untyped(Literal::Number(number.to_string())),
	"\"" <string: "r[.]*"> "\"" => AstNode::typed(Literal::String(string.to_string()), 
					SmplType::String),
};

pub Ident: Ident = {
	<ident: r"[A-Za-z][A-Za-z0-9]*"> => { Ident(AsciiString::from_str(ident).unwrap()) }
};

pub Path: Path = {
	<first: Ident> <v:("." <Ident>)*> => {
		let mut p = Vec::new();
		p.push(first);
		p.extend_from_slice(&v);
		Path(p)
	}
};

pub Keyword: Keyword = {
	"return" => Keyword::Return,
	"if" => Keyword::If,
	"while" => Keyword::While,
	"break" => Keyword::Break,
	"struct" => Keyword::Struct,
	"fn" => Keyword::Function,
};
