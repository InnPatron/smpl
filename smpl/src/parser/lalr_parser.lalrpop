/// --------------SMPL Grammar----------------
///
///
/// module -> decl_stmt+
/// decl_stmt -> fn_decl | struct_decl
///
///
/// fn_decl -> "fn" ident "(" [param_list]? ")" ["->" path]? block
/// param_list -> param ["," param]*
/// param -> ident ":" path
///
///
/// struct_decl -> "struct" ident "{" struct_field_list "}"
/// struct_field_list -> struct_field "," struct_field_list
///		      -> struct_field ","?
/// struct_field -> ident ":" path
///
///
/// block -> "{" [stmt]*  "}"
/// stmt -> expr ";" | expr_stmt
///
///
/// expr_stmt -> if_stmt | while_stmt | local_var_decl | assignment
/// if_stmt -> "if" expr block
/// while_stmt -> "while" expr block
/// local_var_decl -> "let" ident ":" path  "=" expr ";"
/// assignment -> path "=" expr ";"
///
///
/// expr -> cmp_expr
///
///
/// cmp_expr -> cmp_expr ["&&" | "||"] relative_cmp | relative_cmp
/// relative_cmp -> relative_cmp [">" | ">=" | "<" | "<="] equality_cmp | equality_cmp
/// equality_cmp -> equality_cmp ["==" | "!="] math_expr | math_expr
///
///
/// math_expr -> math_expr ["+" | "-"] factor | factor
/// factor -> factor ["*" | "/" | "%"] term | term
/// term -> "(" expr ")" | expr_leaf
/// expr_leaf -> "(" uni_expr ")" | literal | fn_call | path | struct_init
///
///
/// struct_init -> "init" path "{" struct_field_init_list "}"
/// struct_field_init_list -> struct_field_init "," struct_field_init_list
///		      -> struct_field_init ","?
/// struct_field_init -> ident ":" expr 
/// 
/// literal -> "true" | "false" | [0-9]+[.[0.9]+]? | "[[:ASCII:]]"
///
///
/// fn_call -> ident "(" fn_call_args ")"
/// fn_call_args -> expr ["," expr]*
///
///
/// uni_expr -> ["-" | "!" | "&" | "*"] expr
///
///
/// ident -> [a-zA-Z_] [a-zA-Z0-9_]*
/// path -> ident ["." ident]*

use std::str::FromStr;
use ast::*;
use ascii::*;

grammar;

/// module -> decl_stmt+
pub Module: Module = {
	<decl: DeclStmt+> => {
		Module(decl)
	}
};

/// decl_stmt -> fn_decl | struct_decl
DeclStmt: DeclStmt = {
	<f: FnDecl> => f.into(),
	<s: StructDecl> => s.into(),
};

/// fn_decl -> "fn" ident "(" [param_list]? ")" ["->" path]? block
pub FnDecl: Function = {
	"fn" <name: Ident> "(" <params: FnParameterList?> ")" <return_type: ("->" Path)?> <b: Block> => {
		Function::new(
			name,
			params,
			return_type.map(|r| r.1),
			b,
		)
	}
};

/// param_list -> param ["," param]*
FnParameterList: Vec<FnParameter> = {
	<first: FnParameter> <tail: ("," <FnParameter>)*> => {
		let mut v = vec![first];
		v.extend(tail);
		v
	}
};

/// param -> ident ":" path
FnParameter: FnParameter = {
	<name: Ident> ":"  <param_type: Path> => {
		FnParameter::new(name, param_type)
	}
};

/// struct_decl -> "struct" ident "{" struct_field_list "}"
pub StructDecl: Struct = {
	"struct" <name: Ident> "{" <fields: StructFieldList?>"}" => {
		Struct {
			name: name,
			body: StructBody(fields)
		}
	}
};

/// struct_decl -> "struct" ident "{" struct_field_list "}"
/// struct_field_list -> struct_field "," struct_field_list
///		      -> struct_field ","?
/// struct_field -> ident ":" path
StructFieldList: Vec<StructField> = {
	<first: StructField> "," <tail: StructFieldList> => {
		let mut v = vec![first];
		v.extend(tail);
		v
	},

	<single: StructField> ","? => {
		vec![single]
	}
};

/// struct_field -> ident ":" path
StructField: StructField = {
	<name: Ident> ":" <field_type: Path> => {
		StructField {
			name: name,
			field_type: field_type,
		}
	}
};

/// stmt -> expr ";" | expr_stmt
pub Stmt: Stmt = {
	<e: Expr> ";" => Stmt::Expr(e),
	<e: ExprStmt> => Stmt::ExprStmt(e),
};

/// block -> "{" [stmt]*  "}"
Block: Block = {
	"{" <s: Stmt*> "}" => Block(s)
};

/// expr_stmt -> if_stmt | while_stmt | local_var_decl | assignment
pub ExprStmt: ExprStmt = {
	<if_stmt: IfStmt> => ExprStmt::If(if_stmt),
	<while_stmt: WhileStmt> => ExprStmt::While(while_stmt),
	<local_var_decl: LocalVarDecl> => ExprStmt::LocalVarDecl(local_var_decl),
	<assignment: Assignment> => ExprStmt::Assignment(assignment),
	<return_stmt: ReturnStmt> => ExprStmt::Return(return_stmt),
	"continue;" => ExprStmt::Continue,
	"break;" => ExprStmt::Break,
};

/// return -> "return" expr ";"
ReturnStmt: Option<Expr> = {
	"return" <e: Expr?> ";" => e,
};

/// local_var_decl -> "let" ident ":" path  "=" expr ";"
LocalVarDecl: LocalVarDecl = {
	"let" <name: Ident> ":" <var_type: Path> "=" <e: Expr> ";" => {
		LocalVarDecl::new(
			var_type,
			name,
			e,
		)
	},
};

/// assignment -> path "=" expr ";"
Assignment: Assignment = {
	<name: Path> "=" <e: Expr> ";" => {
		Assignment::new(
			name,
			e,
		)
	}
};

/// if_stmt -> "if" branch ["elif" branch]* ["else" block]?
IfStmt: If = {
	"if" <initial_branch: Branch> <elifs: ("elif" Branch)*> <default_block: ("else" Block)?> => {
		let mut branches = Vec::new();
		branches.push(initial_branch);
		branches.extend(elifs.into_iter().map(|branch| branch.1));

		If {
			branches: branches,
			default_block: default_block.map(|block| block.1)
		}
		
	},
};

/// branch -> expr block
Branch: Branch = {
	<conditional: Expr> <block: Block> => {
		Branch {
			conditional: conditional,
			block: block
		}
	}
};

/// while_stmt -> "while" expr block
WhileStmt: While = {
	"while" <conditional: Expr> <block: Block> => {
		While {
			conditional: conditional,
			block: block
		}
	},
};

/// expr -> cmp_expr
pub Expr: Expr = {	
	<c: CmpExpr> => c,
};

/// cmp_expr -> cmp_expr ["&&" | "||"] relative_cmp | relative_cmp
pub CmpExpr: Expr = {
	<lhs: CmpExpr> <op: CmpOpLowestPrecedence> <rhs: RelativeCmpBranch> => {
		Expr::Bin(BinExpr {
			op: op,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},

	RelativeCmpBranch,
};

/// relative_cmp -> relative_cmp [">" | ">=" | "<" | "<="] equality_cmp | equality_cmp
RelativeCmpBranch: Expr = {
	<lhs: RelativeCmpBranch> <op: CmpOpLowPrecedence> <rhs: EqualityCmpBranch> => {
		Expr::Bin(BinExpr {
			op: op,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},

	EqualityCmpBranch,
};

/// equality_cmp -> equality_cmp ["==" | "!="] math_expr | math_expr
EqualityCmpBranch: Expr = {
	<lhs: EqualityCmpBranch> <op: CmpOpGreaterPrecedence> <rhs: MathExpr> => {
		Expr::Bin(BinExpr {
			op: op,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},

	<MathExpr>,
};

CmpOpLowestPrecedence: BinOp = {
	"&&" => BinOp::LogicalAnd,
	"||" => BinOp::LogicalOr,
};

CmpOpGreaterPrecedence: BinOp = {
	">" => BinOp::Greater,
	"<" => BinOp::Lesser,
	">=" => BinOp::GreaterEq,
	"<=" => BinOp::LesserEq,
};

CmpOpLowPrecedence: BinOp = {
	"==" => BinOp::Eq,
	"!=" => BinOp::InEq,
};

/// math_expr -> math_expr ["+" | "-"] factor | factor
pub MathExpr: Expr = {
	<lhs: MathExpr> "+" <rhs: Factor> => {
		Expr::Bin(BinExpr {
			op: BinOp::Add,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},

	<lhs: MathExpr> "-" <rhs: Factor> => {
		Expr::Bin(BinExpr {
			op: BinOp::Sub,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},

	Factor,
};

/// factor -> factor ["*" | "/" | "%"] term | term
Factor: Expr = {
	<lhs: Factor> "*" <rhs: Term> => {
		Expr::Bin(BinExpr {
			op: BinOp::Mul,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},

	<lhs: Factor> "/" <rhs: Term> => {
		Expr::Bin(BinExpr {
			op: BinOp::Div,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},

	<lhs: Factor> "%" <rhs: Term> => {
		Expr::Bin(BinExpr {
			op: BinOp::Mod,
			lhs: Box::new(lhs),
			rhs: Box::new(rhs),
		})
	},
	<Term>
};

/// term -> "(" expr ")" | expr_leaf
Term: Expr = {
	"(" <e: Expr> ")" => e.into(),
	<e: ExprLeaf> => e,
};

/// expr_leaf -> "(" uni_expr ")" | literal | fn_call | path | struct_init
ExprLeaf: Expr = {
	"(" <u: UniExpr> ")" => Expr::Uni(u),
	<path: Path> => {
		let mut path = path;
		if path.0.len() == 1 {
			Expr::Variable(path.0.pop().unwrap())
		} else {
			Expr::FieldAccess(path)
		}
	},
	<l: Literal> => Expr::Literal(l),
	<f: FnCall> => Expr::FnCall(f),

	<struct_init: StructInit> => Expr::StructInit(struct_init),
};

/// struct_init -> "init" path "{" struct_field_init_list "}"
StructInit: StructInit = {
	"init" <struct_name: Path> "{" <init: StructFieldInitList?> "}" => {
		StructInit {
			struct_name: struct_name,
			field_init: init,
		}
	}
};

/// struct_field_init_list -> struct_field_init "," struct_field_init_list
///		      -> struct_field_init ","?
/// struct_field_init -> ident ":" expr 
StructFieldInitList: Vec<(Ident, Box<Expr>)> = {
	<first: StructFieldInit> "," <tail: StructFieldInitList> => {
		let mut v = vec![first];
		v.extend(tail);
		v
	},

	<single: StructFieldInit> ","? => {
		vec![single]
	},
};

StructFieldInit: (Ident, Box<Expr>) = {
	<name: Ident> ":" <init: Expr> => (name, Box::new(init)),
};

/// fn_call -> ident "(" fn_call_args ")"
FnCall: FnCall = {
	<name: Ident> "(" <args: FnCallArgs?> ")" => {
		FnCall::new(
			name, 
			args,
		)
	}
};

/// fn_call_args -> expr ["," expr]*
FnCallArgs: Vec<Expr> = {
	<first: Expr> <tail: ("," <Expr>)*> => {
		let mut v = vec![first];
		v.extend(tail.into_iter());
		v
	}
};

/// uni_expr -> ["-" | "!" | "&" | "*"] expr
UniExpr: UniExpr = {
	<op: UniOp> <e: Expr> => UniExpr { op: op, expr: Box::new(e) },
};

UniOp: UniOp = {
	"-" => UniOp::Negate,
	"!" => UniOp::LogicalInvert,
	"&" => UniOp::Ref,
	"*" => UniOp::Deref,
};

/// literal -> "true" | "false" | [0-9]+[.[0.9]+]? | "[[:ASCII:]]"
pub Literal: Literal = {
	<number: r"[-]?[0-9]+[.[0-9]*]?"> => {
		if number.contains(".") {
			Literal::Float(number.parse::<f64>().unwrap())
		} else {
			Literal::Int(number.parse::<i64>().unwrap())
		}
	},
	<string: r##"".*""##>  => {
		let mut string = AsciiString::from_str(string).unwrap();
		string.remove(0);
		string.pop();
		Literal::String(string)
	},
	"true" => Literal::Bool(true),
	"false" => Literal::Bool(false), 

};

/// ident -> [a-zA-Z_] [a-zA-Z0-9_]*
Ident: Ident = {
	<ident: r"[A-Za-z_][_A-Za-z0-9]*"> => { Ident(AsciiString::from_str(ident).unwrap()) }
};

/// path -> ident ["." ident]*
Path: Path = {
	<first: Ident> <v:("." <Ident>)*> => {
		let mut p = Vec::new();
		p.push(first);
		p.extend_from_slice(&v);
		Path(p)
	}
};
